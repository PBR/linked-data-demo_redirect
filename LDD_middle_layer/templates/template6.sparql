prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix WUR_ont: <http://www.wur.nl/purl/ontology#>
prefix dcterms: <http://purl.org/dc/terms/>
prefix sio: <http://semanticscience.org/resource/>
prefix to: <http://purl.obolibrary.org/obo/TO#>
prefix WUR_acc: <http://www.wur.nl/purl/accession#>
prefix dbp: <http://dbpedia.org/property/>
prefix dbr: <http://dbpedia.org/resource/>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix om: <http://def.seegrid.csiro.au/ontology/om/om-lite#>

select
  distinct (?url as ?Accession)
  #?pVal ?pTitle ?country
where {
    ?acc a WUR_ont:id .
    ?orPlant WUR_ont:has_id ?acc .
    ?acc dcterms:title ?name .
    bind(concat("<a href=\"", str(?acc), "\">", str(?name), "</a>") as ?url) .
    ?desPlant WUR_ont:descendant_of ?orPlant .

    ?exp a <http://purl.obolibrary.org/obo/AGRO_00000370> .
    ?exp <http://purl.obolibrary.org/obo/RO_0000057> ?desPlant .
    ?exp dbp:location ?country .
    ?exp sio:SIO_000219 ?obs .

    ?obs a om:observation .
    ?obs sio:SIO_000332 ?desPlant .
    ?obs to:has_phenotype_variable ?trait .
    ?trait a ?pVar .
    ?trait dcterms:title ?pTitle .
    ?trait to:has_phenotype_score ?pVal .

    VALUES ?country {<{{country}}>} .
    VALUES ?pTitle {"{{phenotypeVariable}}"} .
    VALUES ?pVal {"{{phenotypeValue}}"} .

    #VALUES ?country {dbr:Israel} .
    #VALUES ?pTitle {"Fruit color"} .
    #VALUES ?pVal {"red"} .
}
